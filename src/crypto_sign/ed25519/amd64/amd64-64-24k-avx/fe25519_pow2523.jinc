require "sqr4.jinc"
require "mul4.jinc"

/**
 * function fe25519_pow2523
 * @param {reg u64[4]} x - Field element in 64-bit limb representation
 * @returns {reg u64[4]} - Input**(2**(252)-3) in 64-bit limb representation
 * 
 * This function exponentiates a field element by 2^252 - 3.
 * This is used to compute a squareroot in the prime field. The input is consumed.
*/
inline fn fe25519_pow2523(reg u64[4] x) -> reg u64[4]
{
	// Multiples of the input. z2=z^2, z2_5_0 = z^31=z^(2^5-2^0)
	reg u64[4] z2 z11 z2_5_0 z2_10_0 z2_20_0 z2_50_0 z2_100_0 t;
	// Stack variables of the corresponding reg variables
	stack u64[4] xs z2s z9s z2_5_0s z2_10_0s z2_20_0s z2_50_0s z2_100_0s;
	reg u64[4] r; // Result
	inline int i;
	
	// Copy input 
	xs = #copy(x);
	// x^2
	z2 = __sqr4_rr(x);
	z2s = #copy(z2);
	// x^8
	t = __sqr4_rr(z2);
	t = __sqr4_rr(t);
	// x^9
	z9s = __mul4_ssr(xs, t);
	// x^11
	z11 = __mul4_rss(z9s, z2s);
	// x^22
	t = __sqr4_rr(z11);
	// x^31
	z2_5_0 = __mul4_rsr(z9s, t);
	z2_5_0s = #copy(z2_5_0);
	
	// x^((2^10)-(2^5))
	t = __sqr4_rr(z2_5_0);
	for i=1 to 5{
		t = __sqr4_rr(t);
	}
	// x^((2^10)-1)
	z2_10_0 = __mul4_rsr(z2_5_0s, t);
	z2_10_0s = #copy(z2_10_0);
	
	// x^((2^20)-(2^10))
	t = __sqr4_rr(z2_10_0);
	for i=1 to 10{
		t = __sqr4_rr(t);
	}
	// x^((2^20)-1)
	z2_20_0 = __mul4_rsr(z2_10_0s, t);
	z2_20_0s = #copy(z2_20_0);
	
	// x^((2^40)-(2^20))
	t = __sqr4_rr(z2_20_0);
	for i=1 to 20{
		t = __sqr4_rr(t);
	}
	// x^((2^40)-1)
	t = __mul4_rsr(z2_20_0s, t);
	
	// x^((2^50)-(2^10))
	t = __sqr4_rr(t);
	for i=1 to 10{
		t = __sqr4_rr(t);
	}
	// x^((2^50)-1)
	z2_50_0 = __mul4_rsr(z2_10_0s, t);
	z2_50_0s = #copy(z2_50_0);
	
	// x^((2^100)-(2^50))
	t = __sqr4_rr(z2_50_0);
	for i=1 to 50{
		t = __sqr4_rr(t);
	}
	// x^((2^100)-1)
	z2_100_0 = __mul4_rsr(z2_50_0s, t);
	z2_100_0s = #copy(z2_100_0);
	
	// x^((2^200)-(2^100))
	t = __sqr4_rr(z2_100_0);
	for i=1 to 100{
		t = __sqr4_rr(t);
	}
	// x^((2^200)-1)
	t = __mul4_rsr(z2_100_0s, t);
	
	// x^((2^250)-(2^50))
	t = __sqr4_rr(t);
	for i=1 to 50{
		t = __sqr4_rr(t);
	}
	// x^((2^250)-1)
	t = __mul4_rsr(z2_50_0s, t);
	// x^((2^252)-4)
	t = __sqr4_rr(t);
	t = __sqr4_rr(t);
	// x^((2^252)-3)
	r = __mul4_rsr(xs, t);
	
	return r;
}
