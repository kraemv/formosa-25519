require "reduce4.jinc"

// Multiply limb 0
inline fn __mul4_c0
( reg u64    f0,
  reg u64[4] g,
  reg u64    z, // zero
  reg bool   cf of // cf = 0 and of = 0
  ) ->
  reg u64[4],
  reg u64[4],
  reg bool,
  reg bool
{
  reg u64 lo;
  reg u64[4] h r;

  (h[1], h[0]) = #MULX ( f0, g[0] );

  ( h[2], lo ) = #MULX ( f0, g[1] );
    cf, h[1]   = #ADCX ( h[1], lo, cf );

  ( h[3], lo ) = #MULX ( f0, g[2] );
    cf, h[2]   = #ADCX ( h[2], lo, cf );

  ( r[0], lo ) = #MULX ( f0, g[3] );
    cf, h[3]   = #ADCX ( h[3], lo, cf );

  cf, r[0] = #ADCX ( r[0], z, cf ); // cf = 0

  return h, r, cf, of;
}

// Multiply limb 1
inline fn __mul4_c1
( reg u64[4] h,
  reg u64[4] r,
  reg u64    f,
  reg u64[4] g,
  reg u64    z, // zero
  reg bool   cf of // cf = 0 and of = 0
  ) ->
  reg u64[4],
  reg u64[4],
  reg bool,
  reg bool
{
  reg u64 hi lo;

  ( hi, lo )   = #MULX ( f, g[0] );
  of, h[1]     = #ADOX ( h[1], lo, of );
  cf, h[2]     = #ADCX ( h[2], hi, cf );

  ( hi, lo )   = #MULX ( f, g[1] );
  of, h[2]     = #ADOX ( h[2], lo, of );
  cf, h[3]     = #ADCX ( h[3], hi, cf );

  ( hi, lo )   = #MULX ( f, g[2] );
  of, h[3]     = #ADOX ( h[3], lo, of );
  cf, r[0]     = #ADCX ( r[0], hi, cf );

  ( r[1], lo ) = #MULX ( f, g[3] );
  of, r[0]     = #ADOX ( r[0], lo, of);

  cf, r[1]     = #ADCX ( r[1], z, cf);
  of, r[1]     = #ADOX ( r[1], z, of);

  return h, r, cf, of;
}

// Multiply limb 2
inline fn __mul4_c2
( reg u64[4] h,
  reg u64[4] r,
  reg u64    f,
  reg u64[4] g,
  reg u64    z, // zero
  reg bool   cf of // cf = 0 and of = 0
  ) ->
  reg u64[4],
  reg u64[4],
  reg bool,
  reg bool
{
  reg u64 hi lo;

  ( hi, lo )   = #MULX ( f, g[0] );
  of, h[2]     = #ADOX ( h[2], lo, of );
  cf, h[3]     = #ADCX ( h[3], hi, cf );

  ( hi, lo )   = #MULX ( f, g[1] );
  of, h[3]     = #ADOX ( h[3], lo, of );
  cf, r[0]     = #ADCX ( r[0], hi, cf );

  ( hi, lo )   = #MULX ( f, g[2] );
  of, r[0]     = #ADOX ( r[0], lo, of );
  cf, r[1]     = #ADCX ( r[1], hi, cf );

  ( r[2], lo ) = #MULX ( f, g[3] );
  of, r[1]     = #ADOX ( r[1], lo, of);

  cf, r[2]     = #ADCX ( r[2], z, cf);
  of, r[2]     = #ADOX ( r[2], z, of);

  return h, r, cf, of;
}

// Multiply limb 3
inline fn __mul4_c3
( reg u64[4] h,
  reg u64[4] r,
  reg u64    f,
  reg u64[4] g,
  reg u64    z, // zero
  reg bool   cf of // cf = 0 and of = 0
  ) ->
  reg u64[4],
  reg u64[4],
  reg bool,
  reg bool
{
  reg u64 hi lo;

  ( hi, lo )   = #MULX ( f, g[0] );
  of, h[3]     = #ADOX ( h[3], lo, of );
  cf, r[0]     = #ADCX ( r[0], hi, cf );

  ( hi, lo )   = #MULX ( f, g[1] );
  of, r[0]     = #ADOX ( r[0], lo, of );
  cf, r[1]     = #ADCX ( r[1], hi, cf );

  ( hi, lo )   = #MULX ( f, g[2] );
  of, r[1]     = #ADOX ( r[1], lo, of );
  cf, r[2]     = #ADCX ( r[2], hi, cf );

  ( r[3], lo ) = #MULX ( f, g[3] );
  of, r[2]     = #ADOX ( r[2], lo, of);

  cf, r[3]     = #ADCX ( r[3], z, cf);
  of, r[3]     = #ADOX ( r[3], z, of);

  return h, r, cf, of;
}

/**
 * function __mul4_rsr
 * @param {stack u64[4]} fs - ptr to first factor in 64-bit limb representation
 * @param {reg u64[4]} g - second factor in 64-bit limb representation
 * @returns {reg u64[4]} - Product of f and g in 64-bit limb representation
 * 
 * This function multiplies the two numbers in f and g modulo p. Inputs are not consumed.
 * The representation is:
 * h = f * g
 * h = (2**0*f0 + 2**64*f1 + 2**128*f2 + 2**192*f3) *
 *     (2**0*g0 + 2**64*g1 + 2**128*g2 + 2**192*g3) mod p
*/
inline fn __mul4_rsr(stack u64[4] fs, reg u64[4] g) -> reg u64[4]
{
  reg u64 _38 f z;
  reg u64[4] h r;
  reg bool cf of;
  
  // Setup
  of, cf, _, _, _, z = #set0();
  
  // (r 2^256 + h) = fs*g
  f = fs[0];
  h, r, cf, of = __mul4_c0(      f, g, z, cf, of);

  f = fs[1];
  h, r, cf, of = __mul4_c1(h, r, f, g, z, cf, of);

  f = fs[2];
  h, r, cf, of = __mul4_c2(h, r, f, g, z, cf, of);

  f = fs[3];
  h, r, cf, of = __mul4_c3(h, r, f, g, z, cf, of);
  
  // Reduce modulo p
  _38 = 38;
  h = __reduce4(h, r, _38, z, cf, of);

  return h;
}

/**
 * function __mul4_ssr
 * @param {stack u64[4]} fs - ptr to first factor in 64-bit limb representation
 * @param {reg u64[4]} g - second factor in 64-bit limb representation
 * @returns {stack u64[4]} - ptr to product of f and g in 64-bit limb representation
 * 
 * This function multiplies the two numbers in f and g modulo p. Inputs are not consumed.
 * The representation is:
 * h = f * g
 * h = (2**0*f0 + 2**64*f1 + 2**128*f2 + 2**192*f3) *
 *     (2**0*g0 + 2**64*g1 + 2**128*g2 + 2**192*g3) mod p
*/
inline fn __mul4_ssr(stack u64[4] fs, reg u64[4] g) -> stack u64[4]
{
  stack u64[4] hs;
  reg u64[4] h;

  h = __mul4_rsr(fs, g);
  hs = #copy(h);

  return hs;
}

/**
 * function __mul4_sss
 * @param {stack u64[4]} fs - ptr to first factor in 64-bit limb representation
 * @param {stack u64[4]} gs - ptr to second factor in 64-bit limb representation
 * @returns {stack u64[4]} - ptr to product of f and g in 64-bit limb representation
 * 
 * This function multiplies the two numbers in f and g modulo p. Inputs are not consumed.
 * The representation is:
 * h = f * g
 * h = (2**0*f0 + 2**64*f1 + 2**128*f2 + 2**192*f3) *
 *     (2**0*g0 + 2**64*g1 + 2**128*g2 + 2**192*g3) mod p
*/
inline fn __mul4_sss(stack u64[4] fs gs) -> stack u64[4]
{
  stack u64[4] hs;
  reg u64[4] h g;

  g = #copy(gs);
  h = __mul4_rsr(fs, g);
  hs = #copy(h);

  return hs;
}

/**
 * function __mul4_rss
 * @param {stack u64[4]} fs - ptr to first factor in 64-bit limb representation
 * @param {stack u64[4]} gs - ptr to second factor in 64-bit limb representation
 * @returns {reg u64[4]} - Product of f and g in 64-bit limb representation
 * 
 * This function multiplies the two numbers in f and g modulo p. Inputs are not consumed.
 * The representation is:
 * h = f * g
 * h = (2**0*f0 + 2**64*f1 + 2**128*f2 + 2**192*f3) *
 *     (2**0*g0 + 2**64*g1 + 2**128*g2 + 2**192*g3) mod p
*/

inline fn __mul4_rss(stack u64[4] fs gs) -> reg u64[4]
{
  reg u64[4] h g;

  g = #copy(gs);
  h = __mul4_rsr(fs, g);

  return h;
}
