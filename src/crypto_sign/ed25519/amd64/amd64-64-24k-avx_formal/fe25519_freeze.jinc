abstract predicate bool eqmod(int, int, tuple);
abstract predicate tuple single(int);
abstract predicate tuple quad(u64, u64, u64, u64);
abstract predicate int b2i(bool);
abstract predicate int u64i(u64);
abstract predicate u256 limbs_4u64(tuple);
abstract predicate int pow(int, int);

fn __freeze4_rr(reg u64[4] r_in) -> reg u64[4]
	ensures #[prover=smt] {
     limbs_4u64(quad(result.0[0], result.0[1], result.0[2], result.0[3])) < 0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffed
  }
	ensures #[prover=cas] {
  eqmod (
     \sum (ii \in 0:4) (pow(2, 64*ii)*u64i(result.0[ii])),
     \sum (ii \in 0:4) (pow(2, 64*ii)*u64i(r_in[ii])),
     single((pow(2,255)) - 19)
    )
  }
{
	reg u64[4] t r;
	reg u64 two63;
	reg bool cf;
	inline int i;
	r = #copy(r_in);
	t = #copy(r);
	two63 = 0x8000000000000000;
	
	cf, t[0] += 19;
	cf, t[1] += 0 + cf;
	cf, t[2] += 0 + cf;
	cf, t[3] += two63 + cf;
	
	for i=0 to 4
	{
		r[i] = #CMOVcc(cf, t[i], r[i]);
	}
	t = #copy(r);
	
	cf, t[0] += 19;
	cf, t[1] += 0 + cf;
	cf, t[2] += 0 + cf;
	cf, t[3] += two63 + cf;
	
	for i=0 to 4
	{
		r[i] = #CMOVcc(cf, t[i], r[i]);
	}
	
	return r;
}

inline fn __freeze4_rs(stack u64[4] rs) -> reg u64[4]
{
	reg u64[4] r;
	
	r = #copy(rs);
	r = __freeze4_rr(r);
	
	return r;
}

inline fn __freeze4_sr(reg u64[4] r) -> stack u64[4]
{
	stack u64[4] h;
	
	r = __freeze4_rr(r);
	h = #copy(r);
	
	return h;
}

inline fn __freeze4_ss(stack u64[4] rs) -> stack u64[4]
{
	reg u64[4] r;
	stack u64[4] h;
	
	r = #copy(rs);
	r = __freeze4_rr(r);
	h = #copy(r);
	
	return h;
}

